@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(absl CONFIG REQUIRED)
find_dependency(spdlog CONFIG REQUIRED)
find_dependency(nlohmann_json CONFIG REQUIRED)
find_dependency(igraph CONFIG REQUIRED)
find_dependency(libleidenalg CONFIG REQUIRED)
if(@LIBPOLICYTS_BUILD_ENVIRONMENTS@)
    find_dependency(boulderdash CONFIG REQUIRED)
    find_dependency(craftworld CONFIG REQUIRED)
    find_dependency(sokoban CONFIG REQUIRED)
    find_dependency(tsp CONFIG REQUIRED)
endif()
if(@LIBPOLICYTS_BUILD_TORCH@)
    find_dependency(Torch CONFIG REQUIRED)
endif()

set(libpolicyts_known_comps static shared)
set(libpolicyts_comp_static NO)
set(libpolicyts_comp_shared NO)
foreach (libpolicyts_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (libpolicyts_comp IN_LIST libpolicyts_known_comps)
        set(libpolicyts_comp_${libpolicyts_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "libpolicyts does not recognize component `${libpolicyts_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (libpolicyts_comp_static AND libpolicyts_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "libpolicyts `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(libpolicyts_static_targets "${CMAKE_CURRENT_LIST_DIR}/libpolicyts-static-targets.cmake")
set(libpolicyts_shared_targets "${CMAKE_CURRENT_LIST_DIR}/libpolicyts-shared-targets.cmake")

macro(libpolicyts_load_targets type)
    if (NOT EXISTS "${libpolicyts_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "libpolicyts `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${libpolicyts_${type}_targets}")
endmacro()

if (libpolicyts_comp_static)
    libpolicyts_load_targets(static)
elseif (libpolicyts_comp_shared)
    libpolicyts_load_targets(shared)
elseif (DEFINED libpolicyts_SHARED_LIBS AND libpolicyts_SHARED_LIBS)
    libpolicyts_load_targets(shared)
elseif (DEFINED libpolicyts_SHARED_LIBS AND NOT libpolicyts_SHARED_LIBS)
    libpolicyts_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${libpolicyts_shared_targets}")
        libpolicyts_load_targets(shared)
    else ()
        libpolicyts_load_targets(static)
    endif ()
else ()
    if (EXISTS "${libpolicyts_static_targets}")
        libpolicyts_load_targets(static)
    else ()
        libpolicyts_load_targets(shared)
    endif ()
endif ()
